// *************************************************************************
//
//            Группа прилагательного для хвоста_be
//
//            гр_прил
// *************************************************************************

@Перевод
{
   @Выбор     прил_мораль =
              прил( %мораль )
	      прил( %эмоции ) ;
   @Выбор     прил_мораль( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              прил( Род, Душа, Падеж, Число )
	      прил( Род, Душа, Падеж, Число ) ;
}
@Перевод
{             // наречие of you = с твоей стороны...
   @Структура мораль_of_сущ = @0[of]         гр_сущ_а ;
   @Структура мораль_of_сущ = @0[со_стороны] гр_сущ_а( %Р ) ;
}
@Перевод
{             // too heavy = слишком тяжелый 
   @Структура гр_прил_слишком =
              @0[too] прил( &S ) ;
   @Структура гр_прил_слишком( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              @0[слишком] прил( Род, Душа, Падеж, Число );
}
@Перевод
{             // near enough = достаточно близко
   @Структура гр_прил_достаточно =
              прил( &S ) @0[enough] ;
   @Структура гр_прил_достаточно( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              @0[достаточно] прил( Род, Душа, Падеж, Число ) ;
}
@Перевод
{
   @Выбор     гр_прил_порог =
              гр_прил_слишком
	      гр_прил_достаточно ;
//	      гр_прил_степени ;
   @Выбор     гр_прил_порог( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              гр_прил_слишком   ( Род, Душа, Падеж, Число )
	      гр_прил_достаточно( Род, Душа, Падеж, Число ) ;
//	      гр_прил_степени   ( Род, Душа, Падеж, Число );
}
@Перевод
{             // for you = для тебя
   @Структура for_сущ = @0[for] гр_сущ_а ;
   @Структура for_сущ = @0[для] гр_сущ_а( %Р ) ;
}
@Перевод
{             // for you to lift = чтобы ты поднял
   @Структура for_фраза = 
              @0[for]   гр_сущ_б  @0[to] гр_глагола( &S1, %Inf ) ;
   @Структура for_фраза = 
              @0[чтобы] гр_сущ_б( &Число, &Лицо, %И, &Род )
	      гр_глагола( %Сов,%глагол,Число,%прошлое,Род,%Актив,@0 ) ;
}
@Перевод
{             // to lift = чтобы поднять
   @Структура чтобы_to_inf = to_inf_akt( &S1 ) ;
   @Структура чтобы_to_inf = @0[чтобы] to_inf_akt ;
}
@Перевод
{
   @Выбор     хвост_прил_порог =
              for_фраза
	      for_сущ
	      чтобы_to_inf
	      ;
   @Выбор     хвост_прил_порог =
              for_фраза
	      for_сущ
	      чтобы_to_inf
	      ;
}
// -------------------------------------------------------------------
@Перевод
{             // very kind to do it = очень милый, сделав это
   @Структура гр_прил1 =
	      _Наречие_степени  прил_мораль _Not to_inf1( &S1 ) ;
   @Структура гр_прил1( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              _Наречие_степени  прил_мораль( Род, Душа, Падеж, Число ) _Not to_inf1 ;
}
@Перевод
{             // ready to start = готовый начать
   @Структура гр_прил3 =
	      _Not прил( &S )                to_inf_akt( &S1 ) ;
   @Структура гр_прил3( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              _Not прил( Род, Душа, Падеж, Число ) to_inf_akt ;
}
@Перевод
{   // ... = очень озабочены, чтобы ты принял
   @Структура гр_прил5 =
	      _Наречие_степени  прил( &S )
	      @0[for] гр_сущ_б @0[to] гр_глагола( &S1, %Inf ) ;
   @Структура гр_прил5( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              _Наречие_степени  прил( Род, Душа, Падеж, Число ) 
              @0[чтобы] гр_сущ_б( &Число1, &Лицо, %И, &Род1 )
	      гр_глагола( %Сов,%глагол,Число1,%прошлое,Род1,%Актив,@0 ) ;
}
@Перевод
{   // ... = очень озабочены, если бы ты принял
   @Структура гр_прил6 =
	      _Наречие_степени  прил( &S )
	      @0[for] гр_сущ_б @0[to] гр_глагола( &S1, %Inf ) ;
   @Структура гр_прил6( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              _Наречие_степени  прил( Род, Душа, Падеж, Число ) 
              @0[если_бы] гр_сущ_б( &Число1, &Лицо, %И, &Род1 )
	      гр_глагола( %Сов,%глагол,Число1,%прошлое,Род1,%Актив,@0 ) ;
}
@Перевод
{             // near enough to see = достаточно близко, чтобы видеть
              // too havy for you to lift = слишком тяжелый, чтобы ты поднял
   @Структура гр_прил7 =
	      _Not гр_прил_порог хвост_прил_порог ;
   @Структура гр_прил7( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              _Not гр_прил_порог( Род, Душа, Падеж, Число ) хвост_прил_порог ;
}
@Перевод
{             // very big = очень большой
   @Структура гр_прил8 =
	      _Not _Наречие_степени  прил( &S ) ;
   @Структура гр_прил8( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
              _Not _Наречие_степени  прил( Род, Душа, Падеж, Число ) ;
}
@Перевод
{
   @Выбор     гр_прил =
	      гр_прил_п
              гр_прил1
	      гр_прил3
              гр_прил5
              гр_прил6
	      гр_прил7
	      гр_прил8
	      прил( &S )
	      гр_прил_п0
	      причастие	      
	      ;
   @Выбор     гр_прил( род &Род, душа &Душа, падеж &Падеж, число &Число ) =
	      гр_прил_п ( Род, Душа, Падеж, Число )
              гр_прил1  ( Род, Душа, Падеж, Число )
	      гр_прил3  ( Род, Душа, Падеж, Число )
	      гр_прил5  ( Род, Душа, Падеж, Число )
	      гр_прил6  ( Род, Душа, Падеж, Число )
	      гр_прил7  ( Род, Душа, Падеж, Число )
	      гр_прил8  ( Род, Душа, Падеж, Число )
	      прил      ( Род, Душа, Падеж, Число )
	      гр_прил_п0( Род, Душа, Падеж, Число )
	      причастие ( Род,       Падеж, Число )
	      ;
}
@Перевод 
{
   @Структура гр_прил_предлог = 
              гр_прил  to_inf_предлог ;
   @Структура гр_прил_предлог( род &Род, душа &Душа, падеж &Падеж, число &Число ) = 
              гр_прил( Род, Душа, Падеж, Число )
	      to_inf_предлог  ( Род, Число ) ;
}

